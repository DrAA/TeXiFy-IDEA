{
    parserClass="nl.rubensten.texifyidea.latex.parser.LatexParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Latex"
    psiImplClassSuffix="Impl"
    psiPackage="nl.rubensten.texifyidea.latex.psi"
    psiImplPackage="nl.rubensten.texifyidea.latex.psi.impl"

    elementTypeHolderClass="nl.rubensten.texifyidea.latex.psi.LatexTypes"
    elementTypeClass="nl.rubensten.texifyidea.latex.psi.LatexElementType"
    tokenTypeClass="nl.rubensten.texifyidea.latex.psi.LatexTokenType"

    tokens=[
        WHITE_SPACE='regexp:\s+'
        DISPLAY_MATH_START='\['
        DISPLAY_MATH_END='\]'
        INLINE_MATH_START='$'
        INLINE_MATH_END='$'
        COMMAND_TOKEN='regexp:\\([a-zA-Z]+|.|\n|\r)'
        COMMENT_TOKEN='regexp:%[^\r\n]*'
        STAR='*'
        NORMAL_TEXT='regexp:[^\\{}%\[\]$]+'
    ]
}



latexFile ::= content*

content ::= no_math_content | math_environment

no_math_content ::= comment | commands | group | open_group | NORMAL_TEXT

commands ::= COMMAND_TOKEN STAR? parameter*

parameter ::= optional_param | required_param

optional_param ::= open_group

required_param ::= group

group ::= OPEN_BRACE content* CLOSE_BRACE

open_group ::= OPEN_BRACKET content* CLOSE_BRACKET

comment ::= COMMENT_TOKEN

math_environment ::= inline_math | display_math

inline_math ::= INLINE_MATH_START (no_math_content)* INLINE_MATH_END

display_math ::= DISPLAY_MATH_START (no_math_content)* DISPLAY_MATH_END
